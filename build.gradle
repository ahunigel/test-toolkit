plugins {
    id "io.freefair.lombok" version "9.0.0"
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
}

sourceCompatibility = 17

repositories {
    mavenCentral()
}

dependencies {
    implementation platform("org.spockframework:spock-bom:2.3-groovy-4.0")
    implementation platform('org.junit:junit-bom:5.13.4')

    implementation 'org.assertj:assertj-core:3.27.5'
    implementation 'org.junit.jupiter:junit-jupiter-api'
    implementation 'org.junit.vintage:junit-vintage-engine'
    implementation 'org.hamcrest:hamcrest:3.0'
    implementation 'org.mockito:mockito-core:5.18.0'
    implementation 'org.spockframework:spock-core'

    testImplementation 'net.bytebuddy:byte-buddy' // allows mocking of classes (in addition to interfaces)
    testImplementation "org.objenesis:objenesis:3.4"
}
// Make all tests use JUnit 5
tasks.withType(Test).configureEach {
    useJUnitPlatform()
}
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ahunigel/${project.name}")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = project.name
                description = 'test toolkit'
                url = "https://github.com/ahunigel/${project.name}"
                developers {
                    developer {
                        id = 'ahunigel'
                        name = 'Nigel Zheng'
                        email = 'ahunigel@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:git@github.com:ahunigel/${project.name}.git"
                    url = "https://github.com/ahunigel/${project.name}"
                }
            }
        }
    }
}
